cbuffer Constants : register(b0)
{
    float4 TimeAndResolution; // x=time, y=res.x, z=res.y, w=fractType
    float4 CameraPos; // xyz=pos, w=is3D (no usado en 2D)
    float4 CameraDirX; // xyz=right   (no usado)
    float4 CameraDirY; // xyz=up      (no usado)
    float4 CameraDirZ; // xyz=forward (no usado)

    float4 ZoomOffset; // x=zoom, y=off.x, z=off.y, w=off.z
    float4 FractalColor; // rgba tint
    float4 BackgroundColor; // rgba background

    float4 FractalC; // x=c.x, y=c.y (no usado aqu?)
    int maxiter;
    float3 FractalParams1; // x=bailout, y=power(unused), z = usesDoublePrecision
    float4 FractalParams2; // x=gamma, y/z/w extras

    float4 Options3D; // x=maxSteps, y=maxDist, z=threshold, w=pause(unused)
    float4 AnimationParams; // x=timeScale, y=speedY(unused), z=swirlSpeed, w=seed(unused)

};

float DE_Mandelbulb(float3 pos, float power)
{
    float3 z = pos;
    float dr = 1.0;
    float r = 0.0;

    const float bailout = 2.0;
    const float bailout2 = bailout * bailout;
    const int iterDE = 100;

    for (int i = 0; i < iterDE; ++i)
    {
        float r2 = dot(z, z);
        r = sqrt(r2);
        if (r2 > bailout2)
            break;

        float theta = acos(z.z / r);
        float phi = atan2(z.y, z.x);
        dr = pow(r, power - 1.0) * power * dr + 1.0;

        float zr = pow(r, power);
        float ntheta = theta * power;
        float nphi = phi * power;
        float sinT = sin(ntheta);

        z = zr * float3(
            sinT * cos(nphi),
            sinT * sin(nphi),
            cos(ntheta)
        ) + pos;
    }

    return 0.5 * log(r) * r / dr;
}


float3 calculateNormal(float3 p, float power)
{
    const float epsilon = 0.001;
    float3 n = float3(
        DE_Mandelbulb(p + float3(epsilon, 0, 0), power) - DE_Mandelbulb(p - float3(epsilon, 0, 0), power),
        DE_Mandelbulb(p + float3(0, epsilon, 0), power) - DE_Mandelbulb(p - float3(0, epsilon, 0), power),
        DE_Mandelbulb(p + float3(0, 0, epsilon), power) - DE_Mandelbulb(p - float3(0, 0, epsilon), power)
    );
    return normalize(n);
}

float4 RenderMandelbulb3D(float2 uv) : SV_Target
{
    // ——— Recupera resolución y UV ———
    float2 resolution = float2(TimeAndResolution.y, TimeAndResolution.z);
    float time = TimeAndResolution.x;

    // ——— Origen y dirección del rayo desde CBuffer ———
    float3 ro = CameraPos.xyz + CameraDirZ.xyz * ZoomOffset.x;
    float3 rd = normalize(
        uv.x * CameraDirX.xyz +
        uv.y * CameraDirY.xyz +
        CameraDirZ.xyz
    );

    // ——— Parámetros de ray‐marching ———
    int maxSteps = int(Options3D.x);
    float maxDist = Options3D.y;
    float thresh = Options3D.z;
    
    float minPower = FractalParams1.y;
    float maxPower = 11.0;

    float sinNormalized = sin(time * 0.5) * 0.5 + 0.5; // Para no lidiar con negativos

    float animatedPower = lerp(minPower, maxPower, sinNormalized);

    float totalDist = 0.0;
    float dist = 0.0;
    int i;

    // ——— Bucle de ray‐marching ———
    for (i = 0; i < maxSteps; ++i)
    {
        float3 p = ro + rd * totalDist;
        dist = DE_Mandelbulb(p, animatedPower);
        totalDist += dist;
        if (dist < thresh || totalDist > maxDist)
            break;
    }

    // ——— Color de Fondo (gradiente cielo‐horizonte) ———
    float gradientFactor = saturate(rd.y * 0.5 + 0.5);
    float3 horizonColor = float3(0.9, 0.8, 0.7);
    float3 bgColor = lerp(
        horizonColor,
        BackgroundColor.xyz,
        gradientFactor
    );
    float4 finalColor = float4(bgColor, 1.0);

    // ——— Si impactó la superficie ———
    if (dist < thresh)
    {
        float3 hitPos = ro + rd * totalDist;
        float3 normal = calculateNormal(hitPos, animatedPower);
        float3 viewDir = normalize(ro - hitPos);
        
        // Luz direccional simple
        float3 lightDir = normalize(float3(0.5, 0.8, -0.3));
        float diffuse = saturate(dot(normal, lightDir));
        float ambient = 0.2;
        
        // Componente Especular (Blinn-Phong)
        float3 halfwayDir = normalize(lightDir + viewDir);
        float specAngle = saturate(dot(normal, halfwayDir));
        float specular = pow(specAngle, 32.0);
        
        float fresnelPower = 4.0; // Potencia del Fresnel, ajústala
        float fresnel = pow(1.0 - saturate(dot(normal, viewDir)), fresnelPower);

        float3 baseColor = FractalColor.xyz;
        float3 specularColor = float3(1.0, 1.0, 1.0);
        float3 fresnelColor = float3(0.8, 0.8, 1.0);
        
        float3 litColor = baseColor * (diffuse + ambient) + specular * specularColor;
        finalColor.rgb = lerp(litColor, fresnelColor, fresnel * 0.5);

        finalColor.rgb = saturate(finalColor.rgb);
    }

    return finalColor;
}

float PI = 3.14159265359;

float DE_MengerSponge(float3 pos, int iterations)
{
    float d = length(max(abs(pos) - 1.0, 0.0));
    float s = 1.0;
    for (int i = 0; i < iterations; ++i)
    {
        s /= 3.0;
        float3 a = fmod(abs(pos), 2.0 * s) - s;
        float da = max(abs(a.x), abs(a.y));
        float db = max(abs(a.y), abs(a.z));
        float dc = max(abs(a.z), abs(a.x));
        d = max(d, -min(da, min(db, dc)));
    }
    return d;
}

float getCross(float3 p, float size)
{
    p = abs(p) - size / 3.0;
    float bx = max(p.y, p.z);
    float by = max(p.x, p.z);
    float bz = max(p.x, p.y);
    return min(min(bx, by), bz);
}

float getInnerMenger(float3 p, float size, int iterations)
{
    float d = Options3D.z;
    float scale = 1.0;
    for (int i = 0; i < iterations; i++)
    {
        float r = size / scale;
        float3 q = fmod(p + r, 2.0 * r) - r;
        d = min(d, getCross(q, r));
        scale *= 3.0;
    }
    return d;
}

float4 map(float3 p, float size, int iterations)
{
    float d = 0.0;
    float3 col = float3(1, 1, 1);

    d = -getInnerMenger(p, size, iterations);

    col = abs(floor(p * 3.0 * size - size) + 0.1);

    return float4(col, d);
}

float4 rayMarch(float3 ro, float3 rd, float size, int iterations, float thresh, float maxDist, int maxSteps)
{
    float dist = 0;
    float3 p;
    float3 col;
    for (int i = 0; i < maxSteps; i++)
    {
        p = ro + rd * dist;
        float4 res = map(p, size, iterations);
        col = res.rgb;
        if (res.w < thresh)
            break;
        dist += res.w;
        if (dist > maxDist)
            break;
    }
    return float4(col, dist);
}

float3 getNormal(float3 p, float size, int iterations, float thresh)
{
    float2 e = float2(thresh, 0.0);
    float d = map(p, size, iterations).w;
    float3 n = d - float3(map(p - e.xyy, size, iterations).w, map(p - e.yxy, size, iterations).w, map(p - e.yyx, size, iterations).w);
    return normalize(n);
}

float calculateShadow(float3 p, float3 lightDir, float size, int iterations, float thresh, float maxDist, int maxSteps)
{
    float t = thresh * 2.0;
    float shadow = 1.0;
    for (int i = 0; i < maxSteps; i++)
    {
        float3 pos = p + lightDir * t;
        float d = DE_MengerSponge(pos / size, iterations) * size;
        if (d < thresh)
        {
            shadow = 0.0;
            break;
        }
        shadow = min(shadow, 10.0 * d / t);
        t += d;
        if (t > maxDist)
            break;
    }
    return shadow;
}


float4 RenderMengerSponge3D(float2 uv)
{
    float3 ro = CameraPos.xyz;
    float3 rd = normalize(uv.x * CameraDirX.xyz + uv.y * CameraDirY.xyz + CameraDirZ.xyz);
    float size = ZoomOffset.x;
    int iterations = maxiter;
    float thresh = Options3D.z;
    float maxDist = Options3D.y;
    int maxSteps = int(Options3D.x);

    float4 res = rayMarch(ro, rd, size, iterations, thresh, maxDist, maxSteps);
    float3 bg = lerp(float3(0.9, 0.8, 0.7), BackgroundColor.xyz, saturate(rd.y * 0.5 + 0.5));

    if (res.w >= maxDist)
        return float4(bg, 1);

    float3 p = ro + rd * res.w;
    float3 normal = getNormal(p, size, iterations, thresh);
    float3 viewDir = normalize(ro - p);

    float3 lightDir = normalize(float3(0.5, 0.8, -0.3)); 
    float diffuse = saturate(dot(normal, lightDir));
    float ambient = 0.2;

// Componente Especular (Blinn-Phong)
    float3 halfwayDir = normalize(lightDir + viewDir);
    float specAngle = saturate(dot(normal, halfwayDir));
    float specular = pow(specAngle, 32.0); 

    // Efecto Fresnel
    float fresnelPower = 5.0;
    float fresnel = pow(1.0 - saturate(dot(normal, viewDir)), fresnelPower);

    float shadow = calculateShadow(p, lightDir, size, iterations, thresh, maxDist, maxSteps);

    float3 baseColor = FractalColor.xyz;
    float3 specularColor = float3(1.0, 1.0, 1.0);
    float3 fresnelColor = float3(1.0, 0.9, 0.8); 

    float3 litColor = baseColor * (diffuse * shadow + ambient) + specular * specularColor * shadow; 
    float3 finalColor = lerp(litColor, fresnelColor, fresnel * 0.6); 

    finalColor = saturate(finalColor);

    return float4(finalColor, 1);
}



RWTexture2D<float4> OutputTex : register(u0);

[numthreads(32, 32, 1)]
void CSMain(uint3 DTid : SV_DispatchThreadID)
{
    uint width, height;
    OutputTex.GetDimensions(width, height);
    if (DTid.x >= width || DTid.y >= height)
        return;

    float2 uv = float2(DTid.xy) / float2(width, height) * 2.0 - 1.0;
    uv.x *= width / (float) height;

    int fractalType = int(TimeAndResolution.w);

    float4 result;

    switch (fractalType)
    {
        case 0: // Mandelbulb
            result = RenderMandelbulb3D(uv);
            break;
        case 1: // Menger Sponge
            result = RenderMengerSponge3D(uv);
            break;
        default:
            result = RenderMandelbulb3D(uv);
            break;
    }

    OutputTex[DTid.xy] = result;
}